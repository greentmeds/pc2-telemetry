description = 'Telemetry tool for Project Cars 2'

buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'de.ralfhergert.telemetry.Telemetry'

dependencies {
	compile "org.slf4j:slf4j-api:1.7.+"
	runtime "org.slf4j:slf4j-simple:1.7.+"

	testCompile "junit:junit:4.12"
}

def detectCurrentRevision() {
	'git log -1 --pretty=format:%H'.execute().text.trim()
}

jar {
	from {configurations.runtime.collect {zipTree(it)}}
	manifest {
		attributes (
			'Main-Class': mainClassName,
			'Implementation-Title': 'Racing Telemetry for Project Cars 2',
			'Implementation-Version': detectCurrentRevision()
		)
	}
}

jacoco {
	toolVersion = "0.8.0"
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}
